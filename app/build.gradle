plugins {
    id 'org.jetbrains.kotlin.android'
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

apply from: '../config/detekt/detekt.gradle'


android {

    namespace 'com.example.marvelapp'
    compileSdk 33

    def apikeyPropertiesFile = rootProject.file("apikey.properties")
    def apikeyProperties = new Properties()
    apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

    signingConfigs {
        release {
            storeFile file('../store/marvel_app_key_store.jks')
            storePassword 'marvel@123'
            keyPassword 'marvel@123'
            keyAlias 'marvel-app'
        }
    }

    defaultConfig {
        applicationId "com.example.marvelapp"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "com.example.marvelapp.CustomTestRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        buildConfigField "String", "PUBLIC_KEY", apikeyProperties['PUBLIC_KEY']
        buildConfigField "String", "PRIVATE_KEY", apikeyProperties['PRIVATE_KEY']
        buildConfigField "String", "BASE_URL", '"https://gateway.marvel.com/v1/public/"'
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            debuggable true
        }
        staging {
            initWith debug
            applicationIdSuffix '.staging'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro',
                    'proguard-rules-staging.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

dependencies {

    implementation project(':core')

    // AndroidX
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Material design
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.9.0'

    // Navigation
    def nav_version = '2.5.3'
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"

    // ViewModel and LiveData
    def lifecycle_version = '2.6.1'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Dagger Hilt
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"

    // Room
    def room_version = '2.5.1'
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"

    // Paging3
    def paging_version = "3.1.1"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Glide
    def glide_version = '4.15.1'
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:32.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Other Libs
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Unit tests
    testImplementation project(':testing')
    testImplementation "androidx.room:room-testing:$room_version"

    // Instrumentation tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

    androidTestImplementation("com.squareup.okhttp3:mockwebserver:4.11.0")

    def espresso_version = '3.3.0'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"

    def fragment_version = '1.4.0'
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"

    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestUtil "androidx.test:orchestrator:1.4.2"

}

kapt {
    correctErrorTypes = true
}